Let's make a game!
  name:Byteclick
  desc:Run a successful hosting company!

Layout
  use default
  costs:show

Resources
  *ticket
  name:Support Ticket
  desc:A support ticket

  *resolvedticket
  name:Resolved Support Ticket
  desc:A problem solved
  req:ticketresolution

Buttons
  *replyticket
  name:Reply to Ticket
  desc:Send the customer a reply
  on click:
    if (have multitasking)
      yield 3 ticket
    else
      yield 1 ticket
    end
  end

Buildings
  *office
  name:Support Office
  desc:An office to house a support team member
  on tick:yield 1 ticket
  cost:10 ticket

  *secondline
  name:Second Line
  desc:An office for second line support engineers
  on tick:
    lose 1 ticket
    yield 1 resolvedticket
  end
  req:ticketresolution
  cost:100 ticket
  cost increase:200%

Upgrades
  *ticketresolution
  name:Ticket Resolution
  desc:Allows you to resolve tickets, solving problems! (Actual solution not included)
  req:1000 ticket:earned
  cost:1000 ticket

  *multitasking
  name:Multitasking
  desc:Emails and phone calls at the same time; more than twice as efficient!
  req:2500 ticket:earned
  cost:2500 ticket

Shinies
  *queueattack
  name:Attack queue
  on click:
    $num = (random(100,1000))
    yield $num ticket
    if (have ticketresolution)
      $res = (random(0,$num))
      lose $res ticket
      yield $res resolvedticket
    end
  end
  movement:anywhere shrink moveRandom
  frequency:120
  frequency variation:60
